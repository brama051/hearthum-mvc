plugins {
    id 'io.spring.dependency-management' version '1.0.0.RELEASE' apply false
    id 'org.sonarqube' version '2.2.1'
}

allprojects {
    group = 'com.vabram.hearthum'
    version = '0.1.0'

    apply plugin: 'idea'

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

configure([project(':hearthum-web'), project(':hearthum-domain')]) {

    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    jacoco {
        toolVersion = '0.7.8'
    }

    repositories {
        maven {
            url 'http://devhub.ag04.com/nexus/content/repositories/releases'
        }
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    dependencies {
        compile 'org.apache.commons:commons-lang3:3.4'

        testCompile "org.dbunit:dbunit:${dbunitVersion}"
        testCompile "com.github.springtestdbunit:spring-test-dbunit:${springTestDbUnitVersion}"
        testCompile 'org.mockito:mockito-core'
        testCompile 'org.javassist:javassist'
        testCompile 'org.easytesting:fest-assert:1.4'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
}